apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "${rootDir}/gradle/ktlint.gradle"
apply from: file("${rootDir}/gradle/artifact-settings.gradle")
apply from: "${rootDir}/gradle/kdoc-settings.gradle"

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

android {
    compileSdkVersion androidVersions.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        consumerProguardFiles 'proguard-rules.pro', "${rootDir}/proguard/proguard-project.pro"

        buildConfigField 'String', "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.ext.versionName)
        buildConfigField 'String', "NAV_NATIVE_SDK_VERSION", String.format("\"%s\"", project.ext.version.mapboxNavigator)
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation dependenciesList.mapboxAnnotations
    api dependenciesList.mapboxAndroidCommon

    api project(':libnavigation-base')
    implementation project(':libnavigation-util')
    implementation project(':libnavigator')
    api dependenciesList.mapboxCommonNative
    runtimeOnly project(':libnavigation-router')
    runtimeOnly project(':libtrip-notification')
    runtimeOnly dependenciesList.mapboxLogger
    implementation project(':libnavigation-metrics')
    implementation dependenciesList.mapboxSdkTurf

    implementation dependenciesList.kotlinStdLib
    implementation dependenciesList.coroutinesAndroid
    implementation dependenciesList.androidStartup

    testImplementation project(':libtesting-utils')
    apply from: "${rootDir}/gradle/unit-testing-dependencies.gradle"
    testImplementation dependenciesList.commonsIO

    testImplementation project(':libnavigation-router')
    testImplementation project(':libtrip-notification')

    androidTestImplementation project(':libtesting-ui')
}

dokkaHtml {
    outputDirectory.set(kdocPath)
    moduleName.set("Core")
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)

            perPackageOption {
                matchingRegex.set("com.mapbox.navigation.core.internal.*")
                suppress.set(true)
            }
        }
    }
}

apply from: "${rootDir}/gradle/track-public-apis.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/publish.gradle"
apply from: "${rootDir}/gradle/download-schema.gradle"
