apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mapbox.maps.token'
apply from: "../gradle/script-git-version.gradle"
apply from: "../gradle/ktlint.gradle"
apply plugin: 'com.google.firebase.crashlytics'
apply from: "gradle/task_generate_icons_preview.gradle"

android {
    compileSdkVersion androidVersions.compileSdkVersion

    def getGitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception exception) {
            return ""
        }
    }

    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    defaultConfig {
        applicationId "com.mapbox.navigation.qa_test_app"
        minSdkVersion androidVersions.carMinSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        multiDexEnabled true
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        release {
            keyAlias System.getenv("CIRCLECI_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file("$project.rootDir/navigation_testapp.keystore")
            storePassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            manifestPlaceholders = [enableCrashReporting: "true"]
            firebaseCrashlytics {
                mappingFileUploadEnabled = true
                nativeSymbolUploadEnabled = true
                strippedNativeLibsDir = 'build/intermediates/stripped_native_libs/debug/release/lib/'
                unstrippedNativeLibsDir = com.mapbox.gradle.NativeDownloadTask.UNSTRIPPED_NATIVE_LIBS_PATH
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // Mapbox Navigation SDK
    implementation project(':libnavigation-android')
    implementation project(':libnavui-status')
    implementation project(':libnavui-dropin')
    implementation "com.mapbox.maps:android:10.16.5"

    // test
    androidTestImplementation project(':libtesting-ui')
    androidTestImplementation dependenciesList.testRunner
    androidTestUtil dependenciesList.testOrchestrator

    // Kotlin support
    implementation dependenciesList.kotlinStdLib

    //Coroutines
    implementation dependenciesList.coroutinesAndroid

    // Support libraries
    implementation dependenciesList.androidXCore
    implementation dependenciesList.materialDesign
    implementation dependenciesList.androidXAppCompat
    implementation dependenciesList.androidXCardView
    implementation dependenciesList.androidXConstraintLayout
    implementation dependenciesList.androidXFragment
    implementation dependenciesList.androidXLifecycleLivedata
    implementation dependenciesList.androidXLifecycleRuntime

    implementation dependenciesList.gmsLocation

    implementation dependenciesList.multidex

    // Leak Canary
    debugImplementation dependenciesList.leakCanaryDebug

    // Crashlytics
    implementation dependenciesList.firebaseCrashlyticsNdk
}