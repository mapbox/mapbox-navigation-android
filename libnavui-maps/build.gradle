apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "${rootDir}/gradle/ktlint.gradle"
apply from: file("${rootDir}/gradle/artifact-settings.gradle")
apply from: "${rootDir}/gradle/kdoc-settings.gradle"

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

android {
    compileSdkVersion androidVersions.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro', "${rootDir}/proguard/proguard-project.pro"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    api project(":libnavui-base")

    // workaround for https://github.com/mapbox/mapbox-maps-android-internal/issues/215
    api (dependenciesList.mapboxMapSdk) {
        exclude group: "com.mapbox.plugin", module: "maps-locationcomponent"
    }
    api "com.mapbox.plugin:maps-location:10.0.0-beta.12"
    api dependenciesList.mapboxSdkTurf

    implementation dependenciesList.androidXAppCompat
    implementation dependenciesList.androidXCardView
    implementation dependenciesList.androidXConstraintLayout
    implementation dependenciesList.kotlinStdLib

    implementation dependenciesList.timber
    implementation dependenciesList.coroutinesAndroid
    implementation dependenciesList.svgParser

    ktlint dependenciesList.ktlint

    apply from: "${rootDir}/gradle/unit-testing-dependencies.gradle"
    testImplementation(project(':libtesting-utils'))
}

dokkaHtml {
    outputDirectory.set(kdocPath)
    moduleName.set("UI Maps")
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)
            perPackageOption {
                matchingRegex.set("com.mapbox.navigation.ui.maps.internal.*")
                suppress.set(true)
            }
        }
    }
}

apply from: "${rootDir}/gradle/track-public-apis.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/publish.gradle"
