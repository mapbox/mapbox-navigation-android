apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "../gradle/ktlint.gradle"
apply from: file("../gradle/artifact-settings.gradle")
apply from: "../gradle/kdoc-settings.gradle"

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

android {
    compileSdkVersion androidVersions.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        consumerProguardFiles 'proguard-rules.pro', "../proguard/proguard-project.pro"

        buildConfigField 'String', "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.ext.versionName)
        buildConfigField 'String', "NAV_NATIVE_SDK_VERSION", String.format("\"%s\"", project.ext.version.mapboxNavigator)
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation dependenciesList.mapboxAnnotations
    api dependenciesList.mapboxAndroidCommon

    api project(':base')
    api project(':utils')
    implementation project(':navigator')
    runtimeOnly project(':notification')
    runtimeOnly dependenciesList.mapboxLogger
    implementation project(':metrics')
    implementation dependenciesList.mapboxSdkTurf
    implementation dependenciesList.mapboxSdkRefreshModels

    implementation dependenciesList.kotlinStdLib
    implementation dependenciesList.coroutinesAndroid
    implementation dependenciesList.androidStartup

    api dependenciesList.androidXLifecycleRuntime
    implementation dependenciesList.androidXFragment
    testImplementation dependenciesList.androidXCore

    testImplementation project(':libtesting-utils')
    testImplementation project(':libtesting-navigation-base')
    testImplementation project(':libtesting-navigation-util')
    apply from: "../gradle/unit-testing-dependencies.gradle"
    testImplementation dependenciesList.commonsIO

    testImplementation project(':notification')

    androidTestImplementation project(':libtesting-ui')
}

dokkaHtmlPartial {
    outputDirectory.set(kdocPath)
    moduleName.set("Core")
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)

            perPackageOption {
                matchingRegex.set("com.mapbox.navigation.core.internal.*")
                suppress.set(true)
            }
        }
    }
}

apply from: "../gradle/track-public-apis.gradle"
apply from: "../gradle/jacoco.gradle"
apply from: "../gradle/kover.gradle"
apply from: "../gradle/publish.gradle"
apply from: "../gradle/download-schema.gradle"
