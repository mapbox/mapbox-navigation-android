apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Mapbox Android Services
    compile 'com.mapbox.mapboxsdk:mapbox-android-services:2.1.0'

    // Logging
    compile 'com.jakewharton.timber:timber:4.5.1'

    // Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'org.mockito:mockito-core:2.7.22'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

// The following is necessary to copy the fixtures into the resources folder,
// guaranteeing that the fixtures are looked for inside the right folders.
// (This is a known issue: https://issuetracker.google.com/issues/37020423.)
task copyResourcesToClassesDebug(type: Copy) {
    from "${projectDir}/src/test/res"
    into "${buildDir}/intermediates/classes/test/debug/res"
}

task copyResourcesToClassesRelease(type: Copy) {
    from "${projectDir}/src/test/res"
    into "${buildDir}/intermediates/classes/test/release/res"
}

tasks.whenTaskAdded { task ->
    if (task.name == "compileDebugUnitTestSources")
        task.dependsOn('copyResourcesToClassesDebug')
    if (task.name == "compileReleaseUnitTestSources")
        task.dependsOn('copyResourcesToClassesRelease')
}

apply from: 'javadoc.gradle'
apply from: '../mvn-push-android.gradle'
apply from: '../checkstyle.gradle'
