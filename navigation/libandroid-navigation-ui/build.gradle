apply plugin: 'com.android.library'

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode rootProject.ext.versionCode
    versionName rootProject.ext.versionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true

    buildConfigField('String', "MAPBOX_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"",
        "mapbox-navigation-ui-android"))
    buildConfigField "String", "MAPBOX_NAVIGATION_EVENTS_USER_AGENT",
        String.format("\"mapbox-navigation-ui-android/%s\"", project.VERSION_NAME)
  }
  configurations {
    javadocDeps
  }
}

dependencies {
  // Navigation SDK
  compile project(':libandroid-navigation')

  // Mapbox Map SDK
  compile(rootProject.ext.dep.mapboxMapSdk + '@aar') {
    transitive = true
    exclude module: 'lost'
    exclude module: 'mapbox-java-geojson'
    exclude module: 'mapbox-android-telemetry'
  }

  // Support libraries
  compile rootProject.ext.dep.supportAppcompatV7
  compile rootProject.ext.dep.supportDesign
  compile rootProject.ext.dep.supportRecyclerView
  compile rootProject.ext.dep.supportConstraintLayout

  // Mapbox plugins
  compile rootProject.ext.dep.locationLayerPlugin

  // Logging
  compile rootProject.ext.dep.timber

  // AWS Polly
  compile rootProject.ext.dep.polly

  // Mapbox Map SDK Javadoc
  javadocDeps rootProject.ext.dep.mapboxMapSdk

  // Unit testing
  testCompile rootProject.ext.dep.junit
  testCompile rootProject.ext.dep.mockito


  androidTestCompile(rootProject.ext.dep.testEspressoCore, {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
}

apply from: 'javadoc.gradle'
apply from: '../mvn-push-android.gradle'
apply from: '../checkstyle.gradle'