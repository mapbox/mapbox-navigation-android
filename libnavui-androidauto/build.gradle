apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "${rootDir}/gradle/ktlint.gradle"
apply from: file("${rootDir}/gradle/artifact-settings.gradle")
apply from: "${rootDir}/gradle/kdoc-settings.gradle"

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

android {
    compileSdkVersion androidVersions.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion androidVersions.carMinSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro', "${rootDir}/proguard/proguard-project.pro"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    // Includes a version of the Mapbox Android Auto extension. This library also includes the
    // Jetpack car library.
    api(dependenciesList.mapboxMapsAndroidAuto)

    // This defines the minimum version of Maps, Navigation, and Search which are included in
    // this SDK. To upgrade the SDK versions, you can specify a newer version in your downstream
    // build.gradle.
    def carNavVersion = "2.6.0-alpha.2"
    def carSearchVersion = "1.0.0-beta.29"
    api("com.mapbox.navigation:android:${carNavVersion}")
    api("com.mapbox.search:mapbox-search-android:${carSearchVersion}")
    implementation("com.mapbox.navigation:ui-app:${carNavVersion}")

    implementation(dependenciesList.androidXAppCompat)
    implementation(dependenciesList.coroutinesCore)
    implementation(dependenciesList.androidXLifecycleRuntime)
    implementation(dependenciesList.androidXLifecycleLivedata)
    implementation(dependenciesList.androidXConstraintLayout)
    implementation(dependenciesList.kotlinStdLib)
    implementation(dependenciesList.coroutinesAndroid)

    apply from: "${rootDir}/gradle/unit-testing-dependencies.gradle"
    testImplementation(project(':libtesting-utils'))
    testImplementation(dependenciesList.androidXArchCoreTesting)

    // Needed for the instrumentation tests in the androidTest module
    androidTestImplementation(dependenciesList.testRunner)
    androidTestImplementation(dependenciesList.testRules)
    androidTestImplementation(dependenciesList.androidxTestCore)
    androidTestImplementation(dependenciesList.mockkAndroid)
}

dokkaHtmlPartial {
    outputDirectory.set(kdocPath)
    moduleName.set("UI Car")
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)
            perPackageOption {
                matchingRegex.set("com.mapbox.navigation.ui.car.internal.*")
                suppress.set(true)
            }
        }
    }
}

apply from: "${rootDir}/gradle/track-public-apis.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/publish.gradle"
