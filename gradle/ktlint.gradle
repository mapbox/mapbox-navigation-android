apply from: "$rootDir/gradle/dependencies.gradle"

configurations {
    ktlint
}

dependencies {
    ktlint dependenciesList.ktlint
}

def inputFiles = project.fileTree(dir: "src", include: "**/*.kt")

task ktlint(type: JavaExec, group: "verification") {
    inputs.files(inputFiles)

    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint

    //Fix https://github.com/pinterest/ktlint/issues/1391
    jvmArgs("--add-opens=java.base/java.lang=ALL-UNNAMED")

    args "-F", "src/**/*.kt"
}