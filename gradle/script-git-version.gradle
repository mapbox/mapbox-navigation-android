// https://hackernoon.com/configuring-android-project-version-name-code-b168952f3323#.fcoiok3lv

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar.grgit:grgit-core:4.1.1'
    }
}

import org.ajoberstar.grgit.Grgit

/**
 * git.describe()
 *
 * Find the most recent tag that is reachable from HEAD. If the tag points to the commit,
 * then only the tag is shown. Otherwise, it suffixes the tag name with the number of additional
 * commits on top of the tagged object and the abbreviated object name of the most recent commit.
 *
 * More info: https://git-scm.com/docs/git-describe
 */

ext {
    gitVersionName = "YOUR VERSION NAME GOES HERE"
    gitVersionCode = -1
    gitVersionCodeTime = -1
    try {
        git = Grgit.open(currentDir: projectDir)
        gitVersionName = git.describe(tags: true)
        gitVersionCode = git.log().size()
        gitVersionCodeTime = git.head().time
    } catch (Exception exception) {
        println("Does your build of mapbox-navigation-android have a Git directory?" +
                "The try/catch in the script-git-version.gradle file is for the situation " +
                "when there is no Git setup. Try cloning the mapbox-navigation-android repo, " +
                "rather than downloading the repo as a .zip file.")
    }
}

task printVersion() {
    println("Version Name: $gitVersionName")
    println("Version Code: $gitVersionCode")
    println("Version Code Time: $gitVersionCodeTime")
}
