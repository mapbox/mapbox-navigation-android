ext {
    mapboxArtifactGroupId = 'com.mapbox.navigationcore'
    mapboxArtifactTitle = project.property('POM_ARTIFACT_TITLE')
    mapboxArtifactDescription = project.property('POM_DESCRIPTION')
    mapboxDeveloperName = 'Mapbox'
    mapboxDeveloperId = 'mapbox'
    mapboxArtifactUrl = 'https://github.com/mapbox/mapbox-navigation-android'
    mapboxArtifactVcsUrl = 'https://github.com/mapbox/mapbox-navigation-android.git'
    mapboxArtifactScmUrl = 'scm:git@github.com:mapbox/mapbox-navigation-android.git'
    mapboxArtifactLicenseName = 'Mapbox Terms of Service'
    mapboxArtifactLicenseUrl = 'https://www.mapbox.com/legal/tos/'
    snapshot = project.hasProperty("snapshot") ? project.property("snapshot").toBoolean() : false
    releaseTagPrefix = project.hasProperty('RELEASE_TAG_PREFIX') ? project.property('RELEASE_TAG_PREFIX') : 'mapbox-navigation-android_dash-core_'
    versionName = getVersionName()

    def ndkVersionSuffix = ""
    def ndkMajorVersion = project.findProperty("ndkMajor")
    def parentGradle = gradle.parent
    while (ndkMajorVersion == null && parentGradle != null) {
        ndkMajorVersion = parentGradle.rootProject.findProperty("ndkMajor")
        parentGradle = parentGradle.parent
    }
    if (ndkMajorVersion != null) {
        ndkVersionSuffix = "-ndk$ndkMajorVersion"
    }
    
    /**
     * Properties used for artifact publishing.
     *
     * The map consists of:
     * - Key: The name of the Gradle module from which the artifact is built.
     * - Value: A tuple with two elements:
     *     1. The artifact ID.
     *     2. The SDK name used in the SDK registry.
     */
    navSdkArtifactSettings = [
        'libnavigation-android' : new Tuple2("android$ndkVersionSuffix", 'navigation-core-android'),
        'base' : new Tuple2("base$ndkVersionSuffix", 'navigation-core-base'),
        'navigation' : new Tuple2("navigation$ndkVersionSuffix", 'navigation-core-navigation'),
        'copilot' : new Tuple2("copilot$ndkVersionSuffix", 'navigation-core-copilot'),
        'metrics' : new Tuple2("metrics$ndkVersionSuffix", 'navigation-core-metrics'),
        'tripdata' : new Tuple2("tripdata$ndkVersionSuffix", 'navigation-core-tripdata'),
        'utils' : new Tuple2("utils$ndkVersionSuffix", 'navigation-core-utils'),
        'voice' : new Tuple2("voice$ndkVersionSuffix", 'navigation-core-voice'),
        'navigator' : new Tuple2("navigator$ndkVersionSuffix", 'navigation-core-navigator'),
        'ui-base' : new Tuple2("ui-base$ndkVersionSuffix", 'navigation-core-ui-base'),
        'ui-maps' : new Tuple2("ui-maps$ndkVersionSuffix", 'navigation-core-ui-maps'),
        'ui-utils' : new Tuple2("ui-utils$ndkVersionSuffix", 'navigation-core-ui-utils'),
        'notification' : new Tuple2("notification$ndkVersionSuffix", 'navigation-core-notification'),
        'search' : new Tuple2("search$ndkVersionSuffix", 'navigation-core-search'),
        'ui-components' : new Tuple2("ui-components$ndkVersionSuffix", 'navigation-core-ui-components'),
        'androidauto' : new Tuple2("android-auto-components$ndkVersionSuffix", 'navigation-core-androidauto'),
        'libtesting-router' : new Tuple2("test-router$ndkVersionSuffix", 'navigation-core-testing-router'),
        'libnavigation-custom-route' : new Tuple2("customroute$ndkVersionSuffix", 'navigation-core-custom-route'),
        'datainputs' : new Tuple2("datainputs$ndkVersionSuffix", 'navigation-core-datainputs'),
        'adasis' : new Tuple2("adasis$ndkVersionSuffix", 'navigation-core-adasis'),
        'ev' : new Tuple2("ev$ndkVersionSuffix", 'navigation-core-ev'),
        'ev-rangemap' : new Tuple2("ev-rangemap$ndkVersionSuffix", 'navigation-core-ev-rangemap'),
        'ev-ui' : new Tuple2("ev-ui$ndkVersionSuffix", 'navigation-core-ev-ui'),
        'weather' : new Tuple2("weather$ndkVersionSuffix", 'navigation-core-weather'),
        'roadcam' : new Tuple2("roadcam$ndkVersionSuffix", 'navigation-core-roadcam'),
        'roadcam-ui' : new Tuple2("roadcam-ui$ndkVersionSuffix", 'navigation-core-roadcam-ui'),
        'libnavigation-compose' : new Tuple2("compose$ndkVersionSuffix", 'navigation-core-compose'),
        'libnavigation-compose-core' : new Tuple2("compose-core$ndkVersionSuffix", 'navigation-core-compose-core'),
        'libnavigation-compose-foundation' : new Tuple2("compose-foundation$ndkVersionSuffix", 'navigation-core-compose-foundation'),
        'coordination' : new Tuple2("coordination$ndkVersionSuffix", 'navigation-core-coordination'),
        'coordination-full-hd' : new Tuple2("coordination-full-hd$ndkVersionSuffix", 'navigation-core-coordination-full-hd'),
        'core-mapgpt' : new Tuple2("core-mapgpt$ndkVersionSuffix", 'navigation-core-mapgpt-core'),
        'ui-mapgpt' : new Tuple2("ui-mapgpt$ndkVersionSuffix", 'navigation-core-mapgpt-ui'),
        'applemusic-mapgpt' : new Tuple2("applemusic-mapgpt$ndkVersionSuffix", 'navigation-core-mapgpt-applemusic'),
        'driver-notification' : new Tuple2("driver-notification$ndkVersionSuffix", 'navigation-core-driver-notification'),
        'ev-driver-notification' : new Tuple2("ev-driver-notification$ndkVersionSuffix", 'navigation-core-ev-driver-notification'),
        'audio' : new Tuple2("audio$ndkVersionSuffix", 'navigation-core-audio'),
        'noa' : new Tuple2("noa$ndkVersionSuffix", 'navigation-core-noa'),
    ]
}

def getVersionName() {
    if (project.hasProperty('VERSION_NAME')) {
      return project.property('VERSION_NAME')
    }
    def prefix = project.ext.releaseTagPrefix

    def process = new ProcessBuilder("git", "tag", "-l", "${prefix}*", "--sort=-creatordate")
            .redirectErrorStream(true)
            .start()

    def reader = new BufferedReader(new InputStreamReader(process.inputStream))
    def version = reader.lines().toArray().first()

    if (!version.isEmpty()) {
        version = version.substring(prefix.length())
    }
    return project.ext.snapshot ? getSnapshotVersion(version) : getReleaseVersion(version)
}

static def getReleaseVersion(String version) {
    if (version.isEmpty()) {
        return "1.0.0"
    } else {
        return version
    }
}

def getSnapshotVersion(String version) {
    if (version.isEmpty()) {
        return "1.0.0-SNAPSHOT"
    } else {
        def matcher = version =~ /(\d+)\.(\d+)\.(\d+.*)/
        def major = (matcher[0][1] as Integer)
        def isPreRelease
        def patch = 0
        try {
            patch = Integer.parseInt(matcher[0][3])
            isPreRelease = false
        } catch (NumberFormatException ignored) {
            isPreRelease = true
        }
        def minor = (matcher[0][2] as Integer)
        if (!isPreRelease) {
            minor += 1
        }
        def branch = System.getenv("CIRCLE_BRANCH")
        if (!branch || branch == "main" || branch ==~ /release_mapbox-navigation-android_.*/) {
            return "${major}.${minor}.${patch}-SNAPSHOT"
        } else {
            return "${major}.${minor}.${patch}-${branch}-SNAPSHOT"
        }
    }
}
