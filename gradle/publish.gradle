apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply from: file("$rootDir/gradle/artifact-settings.gradle")
apply from: "$rootDir/gradle/kdoc-settings.gradle"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.mapboxArtifactId
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)

                pom.withXml {
                    final mainNode = asNode()
                    mainNode.appendNode('name', project.ext.mapboxArtifactTitle)
                    mainNode.appendNode('description', project.ext.mapboxArtifactDescription)
                    mainNode.appendNode('url', project.ext.mapboxArtifactUrl)

                    final licenseNode = mainNode.appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', project.ext.mapboxArtifactLicenseName)
                    licenseNode.appendNode('url', project.ext.mapboxArtifactLicenseUrl)
                    licenseNode.appendNode('distribution', "repo")

                    final developerNode = mainNode.appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', project.ext.mapboxDeveloperId)
                    developerNode.appendNode('name', project.ext.mapboxDeveloperName)

                    final scmNode = mainNode.appendNode("scm")
                    scmNode.appendNode("connection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("developerConnection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("url", project.ext.mapboxArtifactUrl)
                }
            }
            debug(MavenPublication) {
                from components.findByName('debug')
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.mapboxArtifactId
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)
            }
        }
    }
}

def sdkNameMap = [:]
sdkNameMap["libnavigation-android"] = "navigation-core-android"
sdkNameMap["libnavigation-base"] = "navigation-core-base"
sdkNameMap["libnavigation-core"] = "navigation-core-navigation"
sdkNameMap["libnavigation-copilot"] = "navigation-core-copilot"
sdkNameMap["libnavigation-metrics"] = "navigation-core-metrics"
sdkNameMap["libnavigation-tripdata"] = "navigation-core-tripdata"
sdkNameMap["libnavigation-voice"] = "navigation-core-voice"
sdkNameMap["libnavigator"] = "navigation-core-navigator"
sdkNameMap["libtrip-notification"] = "navigation-core-notification"
sdkNameMap["libnavigation-util"] = "navigation-core-utils"
sdkNameMap["libnavui-base"] = "navigation-core-ui-base"
sdkNameMap["libnavui-maps"] = "navigation-core-ui-maps"
sdkNameMap["ui-components"] = "navigation-core-ui-components"
sdkNameMap["androidauto"] = "navigation-core-androidauto"
sdkNameMap["libnavui-util"] = "navigation-core-ui-utils"
sdkNameMap["libnavui-androidauto"] = "navigation-core-ui-androidauto"
sdkNameMap["libtesting-router"] = "navigation-core-testing-router"
sdkNameMap["libnavigation-custom-route"] = "navigation-core-custom-route"
sdkNameMap["libnavigation-datainputs"] = "navigation-core-datainputs"

registry {
    sdkName = sdkNameMap[project.name]
    production = true
    snapshot = project.ext.snapshot
    override = snapshot
    dryRun = false
    publish = true
    publishMessage = "cc @mapbox/navigation-android"
    publications = ["release"]
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task androidKdocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from kdocPath
}
