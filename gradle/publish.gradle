apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply from: file("../gradle/artifact-settings.gradle")
apply from: "../gradle/kdoc-settings.gradle"

def artifactName = ""
if (project.hasProperty("ndkMajor")) {
    artifactName = "${project.ext.mapboxArtifactId}-ndk${project.property("ndkMajor")}"
} else {
    artifactName = "${project.ext.mapboxArtifactId}"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId project.ext.mapboxArtifactGroupId
                artifactId artifactName
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)

                pom.withXml {
                    final mainNode = asNode()
                    mainNode.appendNode('name', project.ext.mapboxArtifactTitle)
                    mainNode.appendNode('description', project.ext.mapboxArtifactDescription)
                    mainNode.appendNode('url', project.ext.mapboxArtifactUrl)

                    final licenseNode = mainNode.appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', project.ext.mapboxArtifactLicenseName)
                    licenseNode.appendNode('url', project.ext.mapboxArtifactLicenseUrl)
                    licenseNode.appendNode('distribution', "repo")

                    final developerNode = mainNode.appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', project.ext.mapboxDeveloperId)
                    developerNode.appendNode('name', project.ext.mapboxDeveloperName)

                    final scmNode = mainNode.appendNode("scm")
                    scmNode.appendNode("connection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("developerConnection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("url", project.ext.mapboxArtifactUrl)
                }
            }
            debug(MavenPublication) {
                from components.debug
                groupId project.ext.mapboxArtifactGroupId
                artifactId artifactName
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)
            }
        }
    }
}

def sdkNameMap = [:]
sdkNameMap["libnavigation-android"] = "mobile-navigation-android"
sdkNameMap["libnavigation-base"] = "mobile-navigation-base"
sdkNameMap["libnavigation-core"] = "mobile-navigation-core"
sdkNameMap["libnavigation-copilot"] = "mobile-navigation-copilot"
sdkNameMap["libnavigation-metrics"] = "mobile-navigation-metrics"
sdkNameMap["libnavigator"] = "mobile-navigation-navigator"
sdkNameMap["libtrip-notification"] = "mobile-navigation-notification"
sdkNameMap["libnavigation-router"] = "mobile-navigation-router"
sdkNameMap["libnavigation-util"] = "mobile-navigation-utils"
sdkNameMap["libnavui-base"] = "mobile-navigation-ui-base"
sdkNameMap["libnavui-maps"] = "mobile-navigation-ui-maps"
sdkNameMap["libnavui-util"] = "mobile-navigation-ui-utils"
sdkNameMap["libnavui-tripprogress"] = "mobile-navigation-ui-tripprogress"
sdkNameMap["libnavui-maneuver"] = "mobile-navigation-ui-maneuver"
sdkNameMap["libnavui-resources"] = "mobile-navigation-ui-resources"
sdkNameMap["libnavui-voice"] = "mobile-navigation-ui-voice"
sdkNameMap["libnavui-speedlimit"] = "mobile-navigation-ui-speedlimit"
sdkNameMap["libnavui-shield"] = "mobile-navigation-ui-shield"
sdkNameMap["libnavui-status"] = "mobile-navigation-ui-status"
sdkNameMap["libnavui-dropin"] = "mobile-navigation-ui-dropin"
sdkNameMap["libnavui-androidauto"] = "mobile-navigation-ui-androidauto"
sdkNameMap["libnavui-app"] = "mobile-navigation-ui-app"

registry {
    sdkName = sdkNameMap[project.name]
    production = true
    snapshot = project.ext.snapshot
    override = snapshot
    dryRun = false
    publish = true
    publishMessage = "cc @mapbox/navigation-android"
    publications = ["release"]
    additionalPackageSuffixes = ["-ndk27"]
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task androidKdocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from kdocPath
}
