android {
    publishing {
        singleVariant("debug")
        singleVariant("release")
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply from: file("$rootDir/gradle/artifact-settings.gradle")
apply from: file("$rootDir/gradle/artifact-properties-patcher.gradle")
apply from: "$rootDir/gradle/kdoc-settings.gradle"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.navSdkArtifactSettings[project.name].getV1()
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)

                pom.withXml {
                    final mainNode = asNode()
                    mainNode.appendNode('name', project.ext.mapboxArtifactTitle)
                    mainNode.appendNode('description', project.ext.mapboxArtifactDescription)
                    mainNode.appendNode('url', project.ext.mapboxArtifactUrl)

                    final licenseNode = mainNode.appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', project.ext.mapboxArtifactLicenseName)
                    licenseNode.appendNode('url', project.ext.mapboxArtifactLicenseUrl)
                    licenseNode.appendNode('distribution', "repo")

                    final developerNode = mainNode.appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', project.ext.mapboxDeveloperId)
                    developerNode.appendNode('name', project.ext.mapboxDeveloperName)

                    final scmNode = mainNode.appendNode("scm")
                    scmNode.appendNode("connection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("developerConnection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("url", project.ext.mapboxArtifactUrl)

                    project.ext.patchPomDependencies(mainNode)
                }
            }
            debug(MavenPublication) {
                from components.findByName('debug')
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.navSdkArtifactSettings[project.name].getV1()
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)
            }
        }
    }
}

registry {
    sdkName = project.ext.navSdkArtifactSettings[project.name].getV2()
    production = true
    snapshot = project.ext.snapshot
    override = snapshot
    dryRun = false
    publish = true
    publishMessage = "cc @mapbox/navigation-android"
    publications = ["release"]
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task androidKdocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from kdocPath
}

tasks.register("patchArtifactMetadataFileTask") {
    doLast {
        project.ext.patchArtifactMetadataFile()
    }
}

afterEvaluate {
    tasks.matching { it.name.startsWith('generateMetadataFileFor') }.configureEach { task ->
        task.finalizedBy 'patchArtifactMetadataFileTask'
    }

    tasks.matching { it.name ==~ /publish\w+PublicationToMavenLocal/ }.configureEach { task ->
        task.dependsOn 'patchArtifactMetadataFileTask'
    }
}
