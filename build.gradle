apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'

buildscript {
  apply from: "${rootDir}/gradle/dependencies.gradle"

  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2' }
    maven { url 'https://maven.fabric.io/public' }
  }
  dependencies {
    classpath pluginDependencies.gradle
    classpath pluginDependencies.kotlin
    classpath pluginDependencies.coveralls
    classpath pluginDependencies.errorprone
    classpath pluginDependencies.dependencyUpdates
    classpath pluginDependencies.jacoco
    classpath pluginDependencies.playPublisher
    classpath pluginDependencies.googleServices
    classpath pluginDependencies.crashlytics
    classpath pluginDependencies.license
    classpath pluginDependencies.mapboxSdkVersions
    classpath pluginDependencies.mavenPublish
    classpath pluginDependencies.bintray
    classpath pluginDependencies.artifactory
    classpath pluginDependencies.dokka
  }
}

task testReport(type: TestReport, group: 'Build') {
  description = 'Generates an aggregate test report'
  destinationDir = file("${buildDir}/reports/allTests")
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2' }
    maven {
      url 'https://mapbox.bintray.com/mapbox_private'
      credentials {
        username = project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : System.getenv('BINTRAY_USER')
        password = project.hasProperty('BINTRAY_API_KEY') ? project.property('BINTRAY_API_KEY') : System.getenv('BINTRAY_API_KEY')
      }
    }
    maven { url 'https://mapbox.bintray.com/mapbox' }
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/' }
  }

  group = GROUP
  version = VERSION_NAME
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'net.ltgt.errorprone'
  apply from: "${rootDir}/gradle/dependencies.gradle"

  dependencies {
    errorprone dependenciesList.errorprone
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
