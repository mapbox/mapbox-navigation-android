apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.dokka-android'

android {
  compileSdkVersion androidVersions.compileSdkVersion
  buildToolsVersion androidVersions.buildToolsVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true

    buildConfigField('String',
        "MAPBOX_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "mapbox-navigation-ui-android")
    )
    buildConfigField 'String',
        "MAPBOX_NAVIGATION_EVENTS_USER_AGENT", String.format("\"mapbox-navigation-ui-android/%s\"",
        project.VERSION_NAME
    )
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }
}

dependencies {
  // Navigation SDK
  api project(':libandroid-navigation')

  // Mapbox Map SDK
  implementation(dependenciesList.mapboxMapSdk) {
    exclude module: 'lost'
    exclude module: 'mapbox-java-geojson'
    exclude module: 'mapbox-android-telemetry'
  }

  // Support libraries
  implementation dependenciesList.supportDesign
  implementation dependenciesList.supportRecyclerView
  implementation dependenciesList.supportConstraintLayout
  implementation dependenciesList.supportCardView

  // Architecture libraries
  implementation dependenciesList.lifecycleExtensions
  annotationProcessor dependenciesList.lifecycleCompiler

  // Mapbox plugins
  implementation(dependenciesList.locationLayerPlugin) {
    exclude module: 'mapbox-android-services'
    exclude module: 'mapbox-android-sdk'
  }

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // AWS Polly
  implementation dependenciesList.polly

  // Picasso
  implementation dependenciesList.picasso

  // Mapbox Map SDK Javadoc
  javadocDeps dependenciesList.mapboxMapSdk

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.robolectric
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
  moduleName = 'libandroid-navigation-ui'
  outputFormat = 'javadoc'
  outputDirectory = "$buildDir/javadoc"
  // Do not output deprecated members. Applies globally, can be overridden by packageOptions
  skipDeprecated = false

  // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
  reportUndocumented = true

  skipEmptyPackages = true // Do not create index pages for empty packages

  sourceDirs = project.files('src/main/java')

  externalDocumentationLink {
    // Root URL of the generated documentation to link with. The trailing slash is required!
    url = new URL("https://example.com/docs/")
  }
}

apply from: "${rootDir}/gradle/mvn-push-android.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
