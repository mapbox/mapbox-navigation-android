apply plugin: 'com.android.library'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "${rootDir}/gradle/ktlint.gradle"

group = GROUP
version = VERSION_NAME

android {
  compileSdkVersion androidVersions.compileSdkVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true

    consumerProguardFiles 'proguard-consumer.pro'
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
    unitTests.all {
      maxHeapSize = "1024m"
    }
  }

  licenseReport {
    generateHtmlReport = false
    generateJsonReport = true
    copyHtmlReportToAssets = false
    copyJsonReportToAssets = false
  }
}

dependencies {
  //ktlint
  ktlint dependenciesList.ktlint

  // Navigation SDK
  api project(':libandroid-navigation')

  // Mapbox Maps SDK
  implementation dependenciesList.mapboxMapSdk
  implementation dependenciesList.mapboxAnnotationPlugin

  // Support libraries
  implementation dependenciesList.materialDesign
  implementation dependenciesList.androidXRecyclerView
  implementation dependenciesList.androidXConstraintLayout
  implementation dependenciesList.androidXCardView

  // Architecture libraries
  implementation dependenciesList.lifecycleExtensions
  annotationProcessor dependenciesList.lifecycleCompiler

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // Picasso
  implementation dependenciesList.picasso

  // Timber
  implementation dependenciesList.timber

  // Network
  implementation(dependenciesList.okhttp) {
    force = true
  }
  implementation(dependenciesList.okhttpInterceptor) {
    force = true
  }

  // Mapbox Map SDK Javadoc
  javadocDeps dependenciesList.mapboxMapSdk

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.mockk
  testImplementation dependenciesList.robolectric
  testImplementation dependenciesList.json
}

apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/mvn-push-android.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/dependencies-graph.gradle"
apply from: "${rootDir}/gradle/dependency-updates.gradle"