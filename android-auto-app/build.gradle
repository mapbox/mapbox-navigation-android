apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mapbox.maps.token'
apply from: "${rootDir}/gradle/script-git-version.gradle"
apply from: "${rootDir}/gradle/ktlint.gradle"

android {
    compileSdkVersion androidVersions.compileSdkVersion

    def getGitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception exception) {
            return ""
        }
    }

    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    defaultConfig {
        applicationId "com.mapbox.navigation.examples.androidauto"
        minSdkVersion androidVersions.carMinSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // Mapbox Navigation Android Auto SDK
    implementation project(':libnavui-androidauto')

    // This library will depend on the latest stable version.
    implementation("com.mapbox.navigation:ui-dropin:2.9.1")

    // Dependencies needed for this example.
    implementation dependenciesList.androidXAppCompat
}