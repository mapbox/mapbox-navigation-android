apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mapbox.maps.token'
apply from: "../gradle/script-git-version.gradle"
apply from: "../gradle/ktlint.gradle"

android {
    compileSdkPreview  androidVersions.compileSdkVersion

    def getGitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception exception) {
            return ""
        }
    }

    kotlinOptions {
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    defaultConfig {
        applicationId "com.mapbox.navigation.examples.androidauto"
        minSdkVersion androidVersions.carMinSdkVersion
        targetSdkPreview  androidVersions.targetSdkVersion
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // Mapbox Navigation Android Auto SDK
    implementation project(':libnavui-androidauto')

    // This example is used for development so it may depend on unstable versions.
    // Examples based on final versions can be found in the examples repository.
    //   https://github.com/mapbox/mapbox-navigation-android-examples
    implementation("com.mapbox.navigation:ui-dropin:2.10.3")
    implementation("com.mapbox.search:mapbox-search-android:1.0.0-rc.1")

    // Dependencies needed for this example.
    implementation dependenciesList.androidXCore
    implementation dependenciesList.materialDesign
    implementation dependenciesList.androidXAppCompat
    implementation dependenciesList.androidXCardView
    implementation dependenciesList.androidXConstraintLayout
    implementation dependenciesList.androidXFragment
    implementation dependenciesList.androidXLifecycleLivedata
    implementation dependenciesList.androidXLifecycleRuntime
}