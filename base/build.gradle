apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply from: "../gradle/ktlint.gradle"
apply from: file("../gradle/artifact-settings.gradle")
apply from: "../gradle/kdoc-settings.gradle"

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

def nroDefault = project.findProperty("nroDefault")?.toString() ?: "false"
println("Native Route Object default is set to: $nroDefault")

android {
    namespace 'com.mapbox.navigation.base'
    compileSdkVersion androidVersions.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro', "../proguard/proguard-project.pro"

        buildConfigField 'String', "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.ext.versionName)
        buildConfigField 'boolean', "NATIVE_ROUTE_OBJECT_DEFAULT", nroDefault
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation(project(':utils'))
    api dependenciesList.mapboxCommonNative
    api dependenciesList.mapboxSdkDirectionsModels
    api dependenciesList.mapboxSdkServicesCore
    implementation dependenciesList.mapboxSdkServices
    implementation dependenciesList.mapboxSdkGson
    // Navigator
    implementation dependenciesList.mapboxNavigator

    implementation dependenciesList.androidXAnnotation
    implementation dependenciesList.androidXCoreKtx
    implementation dependenciesList.coroutinesAndroid
    implementation dependenciesList.kotlinStdLib

    testImplementation project(':libtesting-utils')
    testImplementation project(':libtesting-navigation-base')
    testImplementation project(':libtesting-navigation-util')
    apply from: "../gradle/unit-testing-dependencies.gradle"
}

dokkaHtmlPartial {
    outputDirectory.set(kdocPath)
    moduleName.set("Base")
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)

            perPackageOption {
                matchingRegex.set("com.mapbox.navigation.base.internal.*")
                suppress.set(true)
            }
        }
    }
}

apply from: "../gradle/track-public-apis.gradle"
apply from: "../gradle/publish.gradle"
apply from: "../gradle/jacoco.gradle"
apply from: "../gradle/kover.gradle"
