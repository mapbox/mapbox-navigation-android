apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'com.mapbox.android.sdk.versions'
apply from: "${rootDir}/gradle/ktlint.gradle"
apply from: file("${rootDir}/gradle/artifact-settings.gradle")

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

dokkaHtml {
  outputDirectory.set(new File(buildDir.path, "kdoc"))
  moduleName.set("UI")
  dokkaSourceSets {
    configureEach {
      reportUndocumented.set(true)

      perPackageOption {
        matchingRegex.set("com.mapbox.navigation.ui.internal.*")
        suppress.set(true)
      }
    }
  }
}

android {
  compileSdkVersion androidVersions.compileSdkVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles 'proguard-rules.pro', "${rootDir}/proguard/proguard-project.pro"
    vectorDrawables.useSupportLibrary = true
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
    unitTests.all {
      maxHeapSize = "1024m"
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  //ktlint
  ktlint dependenciesList.ktlint

  // Navigation SDK
  api project(':libnavigation-core')
  implementation project(':libnavigation-util')

  // Mapbox
  implementation dependenciesList.mapboxSdkServices
  implementation dependenciesList.mapboxSdkTurf

  // Mapbox Maps SDK
  api dependenciesList.mapboxMapSdk

  implementation dependenciesList.mapboxAnnotationPlugin

  // Support libraries
  api dependenciesList.materialDesign
  api dependenciesList.androidXConstraintLayout

  // Architecture libraries
  implementation dependenciesList.lifecycleExtensions
  annotationProcessor dependenciesList.lifecycleCompiler

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // Picasso
  implementation dependenciesList.picasso

  // Timber
  implementation dependenciesList.timber

  // Mapbox Map SDK Javadoc
  javadocDeps dependenciesList.mapboxMapSdk

  // Network
  implementation dependenciesList.okhttpInterceptor

  // Coroutines
  implementation dependenciesList.coroutinesAndroid

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.mockk
  testImplementation dependenciesList.robolectric
  testImplementation dependenciesList.json
  testImplementation dependenciesList.androidxTestCore
  testImplementation dependenciesList.mockwebserver
  testImplementation dependenciesList.coroutinesTestAndroid
  testImplementation project(':libtesting-utils')
}

apply from: "${rootDir}/gradle/track-public-apis.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/publish.gradle"