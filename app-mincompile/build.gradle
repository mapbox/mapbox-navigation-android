apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.mapbox.maps.token'
apply from: "../gradle/script-git-version.gradle"
apply from: "../gradle/ktlint.gradle"
apply plugin: 'com.google.firebase.crashlytics'
//apply plugin: 'com.google.gms.google-services' need to add support for this

android {
    compileSdk androidVersions.minCompileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def getGitHash = { ->
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        } catch (Exception exception) {
            return ""
        }
    }

    defaultConfig {
        applicationId "com.mapbox.navigation.examples.appmincompile"
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.minCompileSdkVersion
        multiDexEnabled true
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        release {
            keyAlias System.getenv("CIRCLECI_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
            storeFile file("$project.rootDir/navigation_testapp.keystore")
            storePassword System.getenv("CIRCLECI_ANDROID_KEYSTORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = false
            manifestPlaceholders = [enableCrashReporting: "false"]
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            manifestPlaceholders = [enableCrashReporting: "true"]
            firebaseCrashlytics {
                mappingFileUploadEnabled = true
                nativeSymbolUploadEnabled = true
                strippedNativeLibsDir = 'build/intermediates/stripped_native_libs/release/out/lib/'
                unstrippedNativeLibsDir = com.mapbox.gradle.NativeDownloadTask.UNSTRIPPED_NATIVE_LIBS_PATH
            }
        }
    }


    lintOptions {
        abortOnError false
    }

    buildFeatures {
        viewBinding true
    }
}

task downloadUnstrippedNativeLibsDir(type: com.mapbox.gradle.NativeDownloadTask) {
    mapboxDependencies = [
            dependenciesList.mapboxMapSdk,
            dependenciesList.mapboxNavigator,
            dependenciesList.mapboxCommonNative
    ]
    sdkRegistryToken = project.hasProperty('SDK_REGISTRY_TOKEN')
            ? project.property('SDK_REGISTRY_TOKEN')
            : System.getenv('SDK_REGISTRY_TOKEN')
}

configurations.all {
    resolutionStrategy {
        force("androidx.core:core-ktx:1.6.0")
        force("androidx.core:core:1.6.0")
        force("androidx.fragment:fragment-ktx:1.3.0")
        force("androidx.work:work-runtime-ktx:2.6.0")
        force("androidx.lifecycle:lifecycle-runtime-ktx:2.2.0")
        force("androidx.lifecycle:lifecycle-livedata-ktx:2.2.0")
        force("androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0")
    }
}

dependencies {
    // Mapbox Navigation SDK
    implementation project(':libnavigation-android')

    // test
    androidTestImplementation project(':libtesting-ui')
    androidTestImplementation dependenciesList.testRunner
    androidTestUtil dependenciesList.testOrchestrator

    // Kotlin support
    implementation dependenciesList.kotlinStdLib

    //Coroutines
    implementation dependenciesList.coroutinesAndroid

    // Support libraries
    implementation dependenciesList.androidXCore
    implementation dependenciesList.materialDesign
    implementation dependenciesList.androidXAppCompat
    implementation dependenciesList.androidXCardView
    implementation dependenciesList.androidXConstraintLayout

    implementation dependenciesList.gmsLocation

    implementation dependenciesList.multidex

    // Leak Canary
    debugImplementation dependenciesList.leakCanaryDebug

    // Crashlytics
    implementation dependenciesList.firebaseCrashlyticsNdk
}
