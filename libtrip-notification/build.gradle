apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.jaredsburrows.license'
apply from: "${rootDir}/gradle/ktlint.gradle"

dokkaHtml {
    outputDirectory.set(new File(buildDir.path, "kdoc"))
    moduleName.set("Notification")
    dokkaSourceSets {
        configureEach {
            reportUndocumented.set(true)

            perPackageOption {
                matchingRegex.set("com.mapbox.navigation.trip.notification.internal.*")
                suppress.set(true)
            }
        }
    }
}

android {
    compileSdkVersion androidVersions.compileSdkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion androidVersions.minSdkVersion
        targetSdkVersion androidVersions.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro', "${rootDir}/proguard/proguard-project.pro"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    compileOnly dependenciesList.mapboxAnnotations
    kapt dependenciesList.mapboxAnnotationsProcessor

    api project(':libnavigation-base')

    //ktlint
    ktlint dependenciesList.ktlint

    implementation dependenciesList.kotlinStdLib
    implementation project(':libnavigation-util')

    implementation dependenciesList.androidXCore

    implementation dependenciesList.coroutinesAndroid

    apply from: "${rootDir}/gradle/unit-testing-dependencies.gradle"
}

apply from: "${rootDir}/gradle/track-public-apis.gradle"
apply from: "${rootDir}/gradle/publish.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"