apply plugin: 'com.android.library'

android {
  compileSdkVersion androidVersions.compileSdkVersion
  buildToolsVersion androidVersions.buildToolsVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    buildConfigField('String',
        "MAPBOX_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "mapbox-navigation-android")
    )
    buildConfigField('String',
        "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.VERSION_NAME)
    )
    buildConfigField "String",
        "MAPBOX_NAVIGATION_EVENTS_USER_AGENT", String.format("\"mapbox-navigation-android/%s\"",
        project.VERSION_NAME
    )
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
  }
}

dependencies {
  // Mapbox Android Services
  api dependenciesList.mapboxServices

  api dependenciesList.mapboxSdkServices
  api dependenciesList.mapboxSdkTurf

  // Support
  implementation dependenciesList.supportAppcompatV7

  // Logging
  implementation dependenciesList.timber

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.hamcrest
  testImplementation dependenciesList.commonsIO
  testImplementation dependenciesList.robolectric
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
  moduleName = 'libandroid-navigation'
  outputFormat = 'javadoc'
  outputDirectory = "$buildDir/javadoc"

  // Do not output deprecated members. Applies globally, can be overridden by packageOptions
  skipDeprecated = false

  // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
  reportUndocumented = true


  skipEmptyPackages = true // Do not create index pages for empty packages
}

apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/mvn-push-android.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply plugin: 'org.jetbrains.dokka-android'
