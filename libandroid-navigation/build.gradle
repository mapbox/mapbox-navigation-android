apply plugin: 'com.android.library'

android {
  compileSdkVersion androidVersions.compileSdkVersion
  buildToolsVersion androidVersions.buildToolsVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    buildConfigField('String',
        "MAPBOX_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "mapbox-navigation-android")
    )
    buildConfigField('String',
        "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.VERSION_NAME)
    )
    buildConfigField "String",
        "MAPBOX_NAVIGATION_EVENTS_USER_AGENT", String.format("\"mapbox-navigation-android/%s\"",
        project.VERSION_NAME
    )
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
  }
}

dependencies {
  // Mapbox Android Services
  api (dependenciesList.mapboxServices) {
    transitive = true
    exclude module: 'mapbox-java-geojson'
    exclude module: 'mapbox-java-core'
    exclude module: 'mapbox-java-services'
  }

  api "com.mapbox.mapboxsdk:mapbox-java-services-directions:3.0.0-SNAPSHOT"
  api "com.mapbox.mapboxsdk:mapbox-java-services-turf:3.0.0-SNAPSHOT"
  // Support
  implementation dependenciesList.supportAppcompatV7

  // Logging
  implementation dependenciesList.timber

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.hamcrest
  testImplementation dependenciesList.commonsIO
  testImplementation dependenciesList.robolectric
}

apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/mvn-push-android.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
